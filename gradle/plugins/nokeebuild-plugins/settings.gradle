plugins {
	id 'dev.gradleplugins.gradle-plugin-development' version '1.6.8'
}

rootProject.name = 'nokeebuild-plugins'

includeBuild '../documentation-kit-plugins'

include 'build-platform'

include 'basics'
include 'build-cache'
include 'build-scan'
include 'continuous-integration'
include 'enterprise'
include 'ide-idea'
include 'jvm-basics'
include 'jvm-strict-compile'
include 'licensing'
include 'performance-testing'
include 'publishing'
include 'shim-plugins'
include 'signing'
include 'testing'
include 'uber-plugins'

gradle.allprojects {
	repositories {
		mavenCentral()
		gradlePluginPortal()
	}

	pluginManager.withPlugin('dev.gradleplugins.java-gradle-plugin') {
		dependencies {
			api platform(project(":build-platform"))
		}
	}

	// Use JUnit 5
	pluginManager.withPlugin('dev.gradleplugins.gradle-plugin-unit-test') {
		test {
			dependencies {
				implementation platform('org.junit:junit-bom:5.8.2')
				implementation 'org.junit.jupiter:junit-jupiter'
			}
			testTasks.configureEach { useJUnitPlatform() }
		}
	}
	pluginManager.withPlugin('dev.gradleplugins.gradle-plugin-functional-test') {
		functionalTest {
			dependencies {
				implementation platform('org.junit:junit-bom:5.8.2')
				implementation 'org.junit.jupiter:junit-jupiter'
			}
			testTasks.configureEach { useJUnitPlatform() }
		}
	}

	// Use mockito
	pluginManager.withPlugin('dev.gradleplugins.gradle-plugin-unit-test') {
		test {
			dependencies {
				implementation 'org.mockito:mockito-core:4.3.1'
				implementation 'org.mockito:mockito-junit-jupiter:4.3.1'
			}
		}
	}

	// Use Gradle Runner Kit
	pluginManager.withPlugin('dev.gradleplugins.gradle-plugin-functional-test') {
		dependencies {
			functionalTestImplementation gradleRunnerKit()
		}
	}
}
