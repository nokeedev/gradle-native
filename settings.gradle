plugins {
	id 'dev.gradleplugins.gradle-plugin-development' version '1.6.2'
}

rootProject.name = 'gradle-native'

includeBuild 'subprojects/templates'
includeBuild 'gradle/plugins/docs-gradle-plugin'
includeBuild 'gradle/plugins/license-gradle-plugin'
includeBuild 'gradle/plugins/nokeebuild-plugins'

/**
 * Adds specified subproject path to this Gradle build.
 *
 * @param path  the subproject path under {@literal subprojects} folder, must not be null
 * @param closure  a subproject {@link ProjectDescriptor} configuration closure, must not be null
 */
void subproject(String path, Closure closure = {}) {
	def tokens = path.split('/')
	def projectName = tokens.collect { name -> name.split('-').collect { it.capitalize() }.join().uncapitalize() }
	def projectPath = projectName.join(':')
	include(projectPath)
	project(":${projectPath}").projectDir = file("subprojects/${path}")
	project(":${projectPath}").buildFileName = "${tokens.join('-')}.gradle"

	closure.delegate = project(":${projectPath}")
	closure.resolveStrategy = Closure.DELEGATE_FIRST
	closure.call(project(":${projectPath}"))
}

subproject('build-adapter-cmake')
subproject('core-exec')
subproject('core-model')
subproject('core-model')
subproject('core-script')
subproject('core-utils')
subproject('distributions')
subproject('distributions/all')
subproject('distributions/bom')
subproject('docs')
subproject('docs/exemplar-kit') {
	buildFileName = 'exemplar-kit.gradle'
}
subproject('gradle-annotation')
subproject('gradle-api')
subproject('gradle-shim')
subproject('ide-base')
subproject('ide-visual-studio')
subproject('ide-xcode')
subproject('internal-smoke-test')
subproject('internal-testing')
subproject('language-base')
subproject('language-c')
subproject('language-cpp')
subproject('language-native')
subproject('language-objective-c')
subproject('language-objective-cpp')
subproject('language-swift')
subproject('platform-base')
subproject('platform-c')
subproject('platform-cpp')
subproject('platform-native')
subproject('platform-objective-c')
subproject('platform-objective-cpp')
subproject('platform-swift')
subproject('platform-ios')
subproject('platform-jni')
subproject('publishing-core')
subproject('runtime-base')
subproject('runtime-native')
subproject('runtime-darwin')
subproject('runtime-windows')
subproject('testing-base')
subproject('testing-native')
subproject('testing-xctest')

buildCache {
	remote(HttpBuildCache) {
		url = 'https://gradle-build-cache.nokee.dev/cache/'
		credentials {
			username = System.getProperty('nokeeBuildCacheUsername')
			password = System.getProperty('nokeeBuildCachePassword')
		}
		enabled = credentials.username != null && credentials.password != null
		push = System.getenv().containsKey('CI')
	}
}

plugins.withId('com.gradle.enterprise') {
	gradleEnterprise {
		buildScan {
			termsOfServiceUrl = "https://gradle.com/terms-of-service"
			termsOfServiceAgree = "yes"

			if (System.getenv('CI')) {
				tag 'CI'
			} else {
				tag 'LOCAL'
			}

			obfuscation {
				username { name -> 'super-human' }
				hostname { host -> 'somewhere' }
				ipAddresses { addresses -> addresses.collect { '1.2.3.4' } }
			}
		}
	}
}
