pluginManagement {
	includeBuild 'gradle/plugins/docs-gradle-plugin'
	includeBuild 'gradle/plugins/jbake-gradle-plugin'
	includeBuild 'gradle/plugins/documentation-kit-plugins'
	includeBuild 'gradle/plugins/nokeebuild-plugins'
	includeBuild 'gradle/plugins/nokeedocs-plugins'
	includeBuild 'gradle/plugins/sample-kit-plugins'
}

plugins {
	id 'dev.gradleplugins.gradle-plugin-development' version '1.6.8'
	id 'nokeebuild.enterprise'
}

rootProject.name = 'gradle-native'

includeBuild 'subprojects/templates'
includeBuild 'gradle/plugins/docs-gradle-plugin'
includeBuild 'gradle/libraries/nokeetest-libraries'
includeBuild 'gradle/libraries/gradle-build-script'
includeBuild 'gradle/libraries/gradle-test-script'

/**
 * Adds specified subproject path to this Gradle build.
 *
 * @param path  the subproject path under {@literal subprojects} folder, must not be null
 * @param closure  a subproject {@link ProjectDescriptor} configuration closure, must not be null
 */
void subproject(String path, Closure closure = {}) {
	def tokens = path.split('/')
	def projectName = tokens.collect { name -> name.split('-').collect { it.capitalize() }.join().uncapitalize() }
	def projectPath = projectName.join(':')
	include(projectPath)
	project(":${projectPath}").projectDir = file("subprojects/${path}")
	project(":${projectPath}").buildFileName = "${tokens.join('-')}.gradle"

	closure.delegate = project(":${projectPath}")
	closure.resolveStrategy = Closure.DELEGATE_FIRST
	closure.call(project(":${projectPath}"))
}

subproject('build-adapter-cmake')
subproject('build-adapter-xcode')
subproject('core-exec')
subproject('core-model')
subproject('core-model')
subproject('core-script')
subproject('core-utils')
subproject('distributions')
subproject('distributions/all')
subproject('distributions/bom')
subproject('distributions-local')
subproject('docs')
subproject('docs/exemplar-kit') {
	buildFileName = 'exemplar-kit.gradle'
}
subproject('gradle-annotation')
subproject('gradle-api')
subproject('gradle-repository-shim')
subproject('gradle-shim')
subproject('ide-base')
subproject('ide-visual-studio')
subproject('ide-xcode')
subproject('internal-smoke-test')
subproject('internal-testing')
subproject('language-base')
subproject('language-c')
subproject('language-cpp')
subproject('language-native')
subproject('language-objective-c')
subproject('language-objective-cpp')
subproject('language-swift')
subproject('nokee-version-management')
subproject('platform-base')
subproject('platform-c')
subproject('platform-cpp')
subproject('platform-native')
subproject('platform-objective-c')
subproject('platform-objective-cpp')
subproject('platform-swift')
subproject('platform-ios')
subproject('platform-jni')
subproject('publishing-core')
subproject('runtime-base')
subproject('runtime-native')
subproject('runtime-darwin')
subproject('runtime-windows')
subproject('testing-base')
subproject('testing-native')
subproject('testing-xctest')
subproject('xcode-ide-kit')

subproject('universal-model-capability-variants')

ModuleDependency templates(Object notation) {
	final ModuleDependency result = (ModuleDependency) buildscript.dependencies.create(notation)
	result.capabilities { ModuleDependencyCapabilitiesHandler capabilities ->
		capabilities.requireCapability("dev.nokee:${result.getName()}-templates:1.0")
	}
}

gradle.allprojects {
	project.ext.templates = this.&templates
}
