plugins {
	id 'java-library'
}

import static dev.gradleplugins.GradleRuntimeCompatibility.*
import nokeebuild.TemplateDependencyModifier

group = 'dev.nokee'
version = '0.1'

java {
	sourceCompatibility = minimumJavaVersionFor(minimumGradleVersion)
	targetCompatibility = minimumJavaVersionFor(minimumGradleVersion)
}

repositories {
	mavenLocal()
	mavenCentral()
	gradlePluginDevelopment()
}

dependencies {
	def templates = TemplateDependencyModifier.forProject(project)
	api templates('dev.nokee.templates:template-jni-greeter:1.0')
	api templates('dev.nokee.templates:template-c-greeter:1.0')
	api templates('dev.nokee.templates:template-cpp-greeter:1.0')
	api templates('dev.nokee.templates:template-objc-greeter:1.0')
	api templates('dev.nokee.templates:template-objcpp-greeter:1.0')
	api templates('dev.nokee.templates:template-swift-greeter:1.0')
}

// Configure source-elements capability
sourceSets.main { SourceSet sourceSet ->
	dependencies.add(sourceSet.apiConfigurationName, 'dev.gradleplugins:gradle-fixtures-source-elements:latest.release')
}

// Configure source-elements annotation processor
sourceSets.main { SourceSet sourceSet ->
	dependencies.add(sourceSet.annotationProcessorConfigurationName, 'dev.gradleplugins:source-elements-annotation-processor:latest.release')

	tasks.named(sourceSet.compileJavaTaskName, JavaCompile) { task ->
		task.options.compilerArgs.add("-AbasePath=" + project.getProjectDir())
	}
}
