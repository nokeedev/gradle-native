plugins {
	id 'nokeebuild.jvm-base'
	id 'java-library'
	id 'maven-publish'
	id 'groovy-base'
	id 'dev.gradleplugins.java-gradle-plugin' // Only for wiring the plugin under test metadata
	id 'nokeebuild.gradle-plugin-unit-test'
}

repositories {
	// Waiting for https://github.com/adammurdoch/ansi-control-sequence-util/issues/2
	jcenter { mavenContent { it.includeModule('net.rubygrapefruit', 'ansi-control-sequence-util') } }
}

// Caveman dependency shading
def shadedConfiguration = configurations.create('shaded')
configurations.compileOnly.extendsFrom(shadedConfiguration)
configurations.testImplementation.extendsFrom(shadedConfiguration)
tasks.named('jar', Jar) { task ->
	task.from({ shadedConfiguration.files.collect { zipTree(it) } })
}

dependencies {
	shaded 'net.rubygrapefruit:ansi-control-sequence-util:0.2'
	implementation "org.apache.commons:commons-exec:${commonsExecVersion}"
	compileOnly gradleApi(minimumGradleVersion)
	implementation "com.google.guava:guava:${guavaVersion}"
	implementation "commons-io:commons-io:${commonsIoVersion}"
	implementation "org.apache.commons:commons-lang3:${commonsLangVersion}"
	implementation project(':coreUtils')

	testImplementation project(':internalTesting')
	testImplementation 'org.fusesource.jansi:jansi:2.0'
	testImplementation gradleFixtures()
	testImplementation gradleApi(minimumGradleVersion)
}

publishing {
	publications {
		library(MavenPublication) {
			from components.java
		}
	}
}
