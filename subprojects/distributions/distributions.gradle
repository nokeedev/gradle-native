plugins {
	id 'lifecycle-base'
}

import groovy.json.JsonOutput

def allPluginProjectPaths = [
	':coreExec',
	':distributions:all',
	':distributions:bom',
	':ideXcode',
	':languageNative',
	':platformBase',
	':platformIos',
	':platformJni',
	':platformNative',
	':runtimeBase',
	':runtimeNative',
	':runtimeDarwin',
	':testingXctest'
]

def publishToStaging = tasks.register('publishToStaging')

// TODO: Add distribution test that ensure gradle-api is not inside any pom (or any dev.gradleplugins artifacts)

def distributionRepository = layout.buildDirectory.dir('repository')
allPluginProjectPaths.each { String projectPath ->
	project(projectPath) { Project proj ->
		def publishToDistribution = proj.tasks.register('publishToDistribution')
		proj.pluginManager.withPlugin('maven-publish') {
			proj.publishing.repositories.maven {
				name = 'distribution'
				url = distributionRepository
			}
			publishToDistribution.configure { dependsOn('publishAllPublicationsToDistributionRepository') }
		}
	}
	publishToStaging.configure { dependsOn("${projectPath}:publishToDistribution") }
}

tasks.register('publishToBintray') {
	dependsOn(publishToStaging)
	finalizedBy("updateBintrayMetadata")
	doLast {
		// Credentials
		def user = resolveProperty(project, "BINTRAY_USER", "dev.nokee.bintray.user")
		def key = resolveProperty(project, "BINTRAY_KEY", "dev.nokee.bintray.key")

		// Upload configurations
		def repository = 'distributions-snapshots'
		def packageName = 'artifacts'
		def override = true
		if (System.getProperties().containsKey('release')) {
			repository = 'distributions'
			packageName = 'dev.nokee:nokee-gradle-plugins'
			override = false
		}

		// Upload files
		distributionRepository.get().asFileTree.matching { PatternFilterable p ->
			p.include("**/${project.version}/**/*")
		}.each {
			def publish = true
			def filePath = distributionRepository.get().asFile.toPath().relativize(it.toPath())

			print "Publishing ${filePath}... "
			if (0 == ['curl', '-T', it, "-u${user}:${key}", "https://api.bintray.com/content/nokeedev/${repository}/${packageName}/${project.version}/${filePath}?publish=${publish?'1':'0'}&override=${override?'1':'0'}"].execute().waitFor()) {
				println "done"
			} else {
				println "failed"
			}
		}
	}
}

tasks.register("updateBintrayMetadata") {
	doLast {
		// Set attributes
		if (System.getProperties().containsKey('release')) {
			// Credentials
			def user = resolveProperty(project, "BINTRAY_USER", "dev.nokee.bintray.user")
			def key = resolveProperty(project, "BINTRAY_KEY", "dev.nokee.bintray.key")
			def repository = 'distributions'
			def packageName = 'dev.nokee:nokee-gradle-plugins'

			def data = ['gradle-plugin': ["dev.nokee.jni-library:${project.group}:platformJni",
										  "dev.nokee.cpp-language:${project.group}:languageNative",
										  "dev.nokee.c-language:${project.group}:languageNative",
										  "dev.nokee.objective-c-language:${project.group}:languageNative",
										  "dev.nokee.objective-cpp-language:${project.group}:languageNative",
										  "dev.nokee.objective-c-ios-application:${project.group}:platformIos",
										  "dev.nokee.objective-c-xctest-test-suite:${project.group}:testingXctest"]]
			data = data.collect { k, v ->
				return [name: k, values: v, type: 'string']
			}

			['curl', '-X', 'POST', '-H', 'Content-Type: application/json', '--data', JsonOutput.toJson(data), "-u${user}:${key}", "https://api.bintray.com/packages/nokeedev/${repository}/${packageName}/versions/${project.version}/attributes"].execute().waitFor()
		}
	}
}

private String resolveProperty(Project project, String envVarKey, String projectPropKey) {
	Object propValue = System.getenv().get(envVarKey);

	if (propValue != null) {
		return propValue.toString();
	}

	propValue = project.findProperty(projectPropKey);
	if (propValue != null) {
		return propValue.toString();
	}

	return null;
}

tasks.register('javadoc', Javadoc) {
	source(allPluginProjectPaths.collect { evaluationDependsOn(it).sourceSets.main.allJava })
	destinationDir = layout.buildDirectory.dir('javadoc').get().asFile
	classpath = files(allPluginProjectPaths.collect { evaluationDependsOn(it).sourceSets.main.compileClasspath })
}
