plugins {
	id 'nokeebuild.java-gradle-plugin'
	id 'nokeebuild.gradle-plugin-unit-test'
	id 'nokeebuild.gradle-plugin-functional-test'
	id 'nokeebuild.gradle-plugin-integration-test'
	id 'nokeebuild.gradle-plugin-smoke-test'
	id 'maven-publish'
	id 'dev.gradleplugins.documentation.api-reference-module'
	id 'nokeedocs.sample-templates'
	id 'antlr'
	id 'java-test-fixtures'
}

dependencies {
	implementation project(':platformBase')
	implementation project(':coreExec') // for build adapter
	implementation project(':coreUtils')
	implementation project(':platformNative')
	implementation project(':runtimeBase')
	implementation project(':runtimeNative')
	implementation project(':runtimeDarwin')
	implementation project(':universalModelCapabilityVariants')
	compileOnly(project(':gradleAnnotation')) { because 'UntrackedTask annotation backport' }
	implementation "com.google.guava:guava:${guavaVersion}"
	implementation "org.apache.commons:commons-lang3:${commonsLangVersion}"
	implementation "commons-io:commons-io:${commonsIoVersion}"
	implementation "com.google.code.gson:gson:${gsonVersion}"
	implementation project(':xcodeIdeKit')
	implementation "org.antlr:antlr4:${antlrVersion}"
	antlr "org.antlr:antlr4:${antlrVersion}"

	testFixturesApi project(":internalTesting")
}
tasks.named('generateGrammarSource', AntlrTask) { task ->
	task.arguments += ['-visitor']
}

gradlePlugin {
	plugins {
		xcodeBuildAdapter {
			id = 'dev.nokee.xcode-build-adapter'
			implementationClass = 'dev.nokee.buildadapter.xcode.internal.plugins.XcodeBuildAdapterPlugin'
		}
		cocoapodsSupport {
			id = 'dev.nokee.cocoapods-support'
			implementationClass = 'dev.nokee.buildadapter.cocoapods.internal.CocoapodSupportPlugin'
		}
	}
}

test {
	dependencies {
		implementation 'dev.nokee:templates'
		implementation templates(project(':xcodeIdeKit'))
		implementation testFixtures(project(':coreExec'))
		implementation "com.google.jimfs:jimfs:${jimfsVersion}"
		implementation templates(project)
		implementation testFixtures(project)
	}
}

integrationTest {
	dependencies {
		implementation 'dev.nokee:templates'
		implementation templates(project(':xcodeIdeKit'))
		implementation testFixtures(project)
		implementation templates(project)
	}
	testTasks.configureEach {
		maxParallelForks = Runtime.runtime.availableProcessors()
		forkEvery 1
		systemProperty 'junit.jupiter.execution.parallel.enabled', true
		systemProperty 'junit.jupiter.execution.parallel.mode.default', 'concurrent'
	}
}

functionalTest {
	dependencies {
		implementation project(':coreExec')
		pluginUnderTestMetadata project(':platformNative')
		implementation project(':platformNative')
		implementation project(':testingBase')
		implementation 'dev.nokee:templates:latest.integration'
		implementation project(':xcodeIdeKit')
		implementation testFixtures(project(':xcodeIdeKit'))
		implementation templates(project(':xcodeIdeKit'))
		implementation templates(project)
		implementation 'com.google.code.gson:gson:2.8.6'
		implementation project(':coreExec')
		implementation testFixtures(project)
	}
}

smokeTest {
	dependencies {
		implementation gradleTestKit(minimumGradleVersion)
		implementation project(':coreExec')
	}
}
