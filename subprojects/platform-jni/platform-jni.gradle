plugins {
	id 'nokeebuild.java-gradle-plugin'
	id 'nokeebuild.gradle-plugin-unit-test'
	id 'nokeebuild.gradle-plugin-functional-test'
	id 'maven-publish'
	id 'dev.gradleplugins.documentation.api-reference-module'
}

gradlePlugin {
	plugins {
		jniLibraryBase {
			id = 'dev.nokee.jni-library-base'
			implementationClass = 'dev.nokee.platform.jni.internal.plugins.JniLibraryBasePlugin'
		}
		jniLibrary {
			id = 'dev.nokee.jni-library'
			implementationClass = 'dev.nokee.platform.jni.internal.plugins.JniLibraryPlugin'
		}
	}

	dependencies {
		api project(':platformBase')
		api project(':platformNative')
		implementation project(':coreUtils')
		implementation project(':runtimeDarwin')
		implementation project(':runtimeNative')
		implementation project(':languageC')
		implementation project(':languageCpp')
		implementation project(':languageObjectiveC')
		implementation project(':languageObjectiveCpp')
		implementation project(':languageSwift')
		implementation project(':platformObjectiveC')
		implementation project(':platformObjectiveCpp')

		implementation "org.apache.commons:commons-lang3:${commonsLangVersion}"
		implementation "com.google.guava:guava:${guavaVersion}"
		implementation "commons-io:commons-io:${commonsIoVersion}"
		implementation "com.google.auto.factory:auto-factory:${autoFactoryVersion}"
		annotationProcessor "com.google.auto.factory:auto-factory:${autoFactoryVersion}"
	}
}

test {
	dependencies {
		implementation testFixtures(project(':platformNative'))
		implementation testFixtures(project(':platformBase'))
		implementation "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.10"
		implementation "dev.gradleplugins:gradle-fixtures-well-behaving-plugins:latest.release"
		implementation testFixtures(project(':languageC'))
		implementation testFixtures(project(':languageCpp'))
		implementation testFixtures(project(':languageObjectiveC'))
		implementation testFixtures(project(':languageObjectiveCpp'))
	}
	testTasks.configureEach {
		maxParallelForks = Runtime.runtime.availableProcessors()
		forkEvery 1
		systemProperty 'junit.jupiter.execution.parallel.enabled', true
		systemProperty 'junit.jupiter.execution.parallel.mode.default', 'concurrent'
	}
}

functionalTest {
	dependencies {
		pluginUnderTestMetadata project(':languageNative')
		pluginUnderTestMetadata project(':platformC')
		pluginUnderTestMetadata project(':platformCpp')

		implementation project(':platformBase')
		implementation project(':platformNative')
		implementation project(':runtimeDarwin')
		implementation project(':runtimeNative')
		implementation 'dev.nokee:templates:latest.integration'
		implementation "commons-io:commons-io:${commonsIoVersion}"
		implementation project(':coreExec')
		implementation testFixtures(project(':languageC'))
		implementation testFixtures(project(':languageCpp'))
		implementation testFixtures(project(':languageObjectiveC'))
		implementation testFixtures(project(':languageObjectiveCpp'))
		implementation testFixtures(project(':platformNative'))
	}
}

// Unfortunately, because of the debt inherited from Gradle around how Windows toolchains are discovered
import org.gradle.internal.os.OperatingSystem
if (OperatingSystem.current().windows) {
	tasks.named('test', Test) {
		forkEvery(1)
	}
}
